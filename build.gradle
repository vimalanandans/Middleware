apply plugin: 'org.sonarqube'
buildscript {
    repositories {
        mavenCentral()
        jcenter {
            url "http://jcenter.bintray.com/"
        }
    }
    ext {
        androidGradleToolsVersion = "2.2.0"
        androidBuildToolsVersion = "24.0.2"
        androidSupportLibraryVersion = "24.2.1"
        androidCompileSdkVersion = "24"
        androidMinSdkVersion = "16"
        androidTargetSdkVersion = "24"
        androidVersionCode = "1"
        androidVersionName = "1.0"
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${project.androidGradleToolsVersion}"
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.1.0')
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.1"
    }
}
allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'distribution'
    apply plugin: 'license'
    group = 'com.bezirk'
    version = "3.1.0"

    ext {
        artifactRepoBase = "https://bezirk.artifactoryonline.com/bezirk"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "${artifactRepoBase}/libs-release-local/"
        }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = "1.7"
        targetCompatibility = "1.7"
    }
    license {
        header rootProject.file('LICENSE')
        include "**/*.java"
        excludes(["**/*.properties", "**/*Test.java", "**/package-info.java"])
        strictCheck true
        ignoreFailures false
    }
}

//Copy files into a zipped distribution
distributions {
    main {
        contents {
            from {
                'build/dist'
            }
            into '/'
        }
    }
}

sonarqube {
    properties {
        def SONARQ_HOST = project.hasProperty('sonar_host') ? "${sonar_host}" : System.getenv('SONARQUBE_HOST')
        def SONARQ_USER = project.hasProperty('sonar_login') ? "${sonar_login}" : System.getenv('SONARQUBE_USER')
        def SONARQ_PASSWORD = project.hasProperty('sonar_password') ? "${sonar_password}" : System.getenv('SONARQUBE_PASSWORD')
        def SONARQ_PROFILE = project.hasProperty('sonar_middleware_profile') ? "${sonar_middleware_profile}" : "Sonar way"
        property "sonar.host.url","${SONARQ_HOST}"
        property "sonar.login", "${SONARQ_USER}"
        property "sonar.password", "${SONARQ_PASSWORD}"
        property "sonar.profile", "${SONARQ_PROFILE}"
        property "sonar.projectKey", "Middleware_Integration"
        property "sonar.projectName", "Bezirk Middleware Integration Branch"
        property "sonar.projectDescription", "Bezirk Middleware Integration Branch"
        property "sonar.projectVersion", "3.0-Alpha"
        property "sonar.projectBaseDir", "."
        property "sonar.sources", "core,java,build"
        property "sonar.exclusions", "**/build/**/.class,**/build/**/*.java,**/src/**/package-info.java"
        property "sonar.language", "java"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"
    }
}
