/**
 * Global project version change history
 * Currently the project alpha version is incremented every time there is a change in any of the middleware artifacts to maintain consistency.
 * 3.0.0-alpha+001 development mode enabled for CMU Iot group (Rishabh)
 * 3.0.0-alpha+002 added policy classes for protocols-penguin (Rishabh)
 * 3.0.0-alpha+003 PreferencesAPI implementation for using sphere configuration (Rishabh)
 * 3.0.0-alpha+004 Removing use of comms.properties & upadevice.properties (Rishabh)
 * 3.0.0-alpha+005 Changing sphere key lengths to prevent adding Unlimited Strength Jurisdiction Policy (Rishabh)
 * 3.0.0-alpha+006 Logging enable/disable using jvm/system property, removing Factory.getInstance(BezirkConfig) (Rishabh)
 * 3.0.0-alpha+007 Enabling development sphere by default and removing system.out.println for evasive events in zyre (Rishabh)
 * 3.0.0-alpha+008 Displaying ethernet configuration for multiple interfaces, issue #77 (Rishabh)
 * 3.0.0-alpha+009 Freezing major api changes & Nits before Mexico Hackathon. (Michael/Rishabh)
 * 3.0.0-alpha+010 Bug with android proxy and unwanted swing thread in java. (Michael/Rishabh)
 * 3.0.0-alpha+011 Fixes issue #91 (first event duplication)(Rishabh)
 * 3.0.0-alpha+012 Changing text display for issue #59 (bezirk ethernet configuration), testApp Pub/Sub testing through different applications until 2-zirk registration issue is resolved (Rishabh)
 * 3.0.0-alpha+013 bezrik -> bezirk for network-util package fix (Rishabh)
 */
buildscript {
    repositories {
        mavenCentral()
        jcenter() //used for jfrog
    }
    ext {
        androidGradleToolsVersion = "2.1.0"
        androidBuildToolsVersion = "23.0.3"
        androidSupportLibraryVersion = "19.+"
        androidCompileSdkVersion = "19"
        androidMinSdkVersion = "16"
        androidTargetSdkVersion = "19"
        androidVersionCode = "1"
        androidVersionName = "1.0"
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${project.androidGradleToolsVersion}"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.1+"
    }
    ext {
        local = true //change to false for compiling project artifacts from remote maven
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven'

    group = 'com.bezirk'
    version = "3.0.0-alpha+013"

    ext {
        artifactRepoBase = "https://bezirk.artifactoryonline.com/bezirk"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "${artifactRepoBase}/libs-release-local/"
            credentials {
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
        }
    }
}

/**
 * Build java distribution. Jars contained in the zip are used to run PC services.
 * Eg. http://52.18.79.103:8090/rishabh.gulati/HelloWorld
 **/
task javaBuildDist(type: Zip) {
    baseName = 'bezirk-java-dist'
    version = project.version
    into 'jars'
    from { project(':core:bezirk-middleware-api').configurations.runtime }
    from { project(':java:libraries:bezirk-factory').configurations.runtime }
    from { project(':core:bezirk-middleware-api').jar }
    from { project(':java:libraries:bezirk-factory').jar }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}