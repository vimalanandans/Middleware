apply plugin: 'com.jfrog.artifactory'
apply plugin: 'maven-publish'

/*
 * Command to publish: gradlew artifactoryPublish
 */
publishing {
    publications {
        bezirkPublication(MavenPublication) {
            from components.java

            artifact javadocJar

            /*uncomment to publish sources jar*/
            //artifact sourceJar
        }
    }
}

artifactory {
    contextUrl = 'https://bezirk.artifactoryonline.com/bezirk'
    publish {
        repository {
            repoKey = 'libs-release-local' // The Artifactory repository key to publish to
            username = project.hasProperty('artifactory_user') ? "${artifactory_user}" : System.getenv('artifactory_user')
            password = project.hasProperty('artifactory_password') ? "${artifactory_password}" : System.getenv('artifactory_password')
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('bezirkPublication')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

//for suppressing doclint issues when publishing with java8
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}